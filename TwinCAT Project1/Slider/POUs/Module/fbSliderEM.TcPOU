<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="fbSliderEM" Id="{8d63ff81-3bc4-421f-8810-eb0a34af2cf8}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK fbSliderEM EXTENDS fbEM
VAR_INPUT
	UnknownStartPosition : BOOL := FALSE;
	StartOnOtherSide : BOOL := FALSE;
END_VAR

VAR
	bSliderSide : BOOL;
	XYMoverActualStep: INT;
	fbSignalSim : fbSignalSimSlider;
	timer : TON;
	timerPT : TIME := T#2S;
END_VAR

VAR RETAIN
	State : INT := 0;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*
	Input/Output mapping
	I[1] <- Siłownik pozycja 1 (wsunięty) 
	I[2] <- Siłownik pozycja 2 (wysunięty)
	I[3] <- Czujnik obecności obiektu (na pozycji 1 (wsunięty)) 
	Q[1] <- Wysuń siłownik (do pozycji 2)
	Q[2] <- Wsuń siłownik (do pozycji 1)
*)

GetUnCmd();
fbSignalSim(
	em := em,
	EnableSim := EnableSim,
	UnknownStartPosition := UnknownStartPosition,
	StartOnOtherSide := StartOnOtherSide,
	CmdSim := CmdSim
);
timer(PT := timerPT);

IF em.Config.Functionality = E_Function.eSlider THEN
	IF State >= 15 AND em.Cmd = E_CMD.abort THEN
		State := 5;
	END_IF
	
	IF State >= 35 AND em.Cmd = E_CMD.stop THEN
		State := 15;
	END_IF

	CASE State OF
		0:	em.StatusHMI.isMoving := FALSE;
			em.StatusHMI.undefined := TRUE;
			IF em.Config.Data.I[1] OR em.Config.Data.I[2] THEN
				State := 1;
				em.StatusHMI.undefined := FALSE;
			ELSE
				em.Status := E_Status.undefined;
				State := 20;
			END_IF
			
		1:	IF em.Config.Data.I[2] THEN
				bSliderSide := TRUE;
				em.StatusHMI.currentPosition := 2;
			ELSE
				em.StatusHMI.currentPosition := 1;
			END_IF
			State := 40;
		
		5:	em.Status := E_Status.aborting;
			em.Config.Data.Q[1] := FALSE;
			em.Config.Data.Q[2] := FALSE;
			em.StatusHMI.isMoving := FALSE;
			em.StatusHMI.undefined := TRUE;
			State := 7;
			
		7:	em.Status := E_Status.aborted;
			State := 10;
			
		10:	IF em.Cmd = E_CMD.clear THEN
				em.Status := E_Status.clearing;
				em.StatusHMI.undefined := FALSE;
				State := 20;
			END_IF
			
		15: em.Status := E_Status.stopping;
			em.Config.Data.Q[1] := FALSE;
			em.Config.Data.Q[2] := FALSE;
			em.StatusHMI.isMoving := FALSE;
			em.StatusHMI.undefined := FALSE;
			State := 20;
			
		20:	em.Status := E_Status.stopped;
			State := 30;
			
		30:	IF em.Cmd = E_CMD.reset THEN
				em.Status := E_Status.resetting;
				em.Config.Data.Q[1] := FALSE;
				em.Config.Data.Q[2] := TRUE;
				em.StatusHMI.isMoving := TRUE;
				em.StatusHMI.movingDirection := E_DIRECTION.Upstream;
				em.StatusHMI.movingToPosition := 1;
				State := 35;
			END_IF
			
		35:	IF em.Config.Data.I[1] THEN
				em.StatusHMI.isMoving := FALSE;
				em.Config.Data.Q[2] := FALSE;
				bSliderSide := FALSE;
				State := 40;
			END_IF
			
		40:	em.Status := E_Status.idle;
			IF em.Cmd = E_CMD.start THEN
				State := 45;
			END_IF
			
		45:	em.Status := E_Status.Starting;
			State := 50;
			
			// Execute direction
		50:	em.Status := E_Status.execute;
			IF bSliderSide AND NOT em.Config.Data.I[3] THEN
				timer(IN := TRUE);
				State := 52;
			ELSIF em.Config.Data.I[3] THEN
				timer(IN := TRUE);
				State := 55;
			END_IF
			
		52: IF timer.Q THEN
				em.StatusHMI.movingDirection := E_DIRECTION.Upstream;
				em.StatusHMI.movingToPosition := 1;
				em.StatusHMI.undefined := FALSE;
				timer(IN := FALSE);
				em.Config.Data.Q[2] := TRUE;
				State := 60;
			END_IF	
			
		55: IF timer.Q THEN
				em.StatusHMI.movingDirection := E_DIRECTION.Downstream;
				em.StatusHMI.movingToPosition := 2;
				em.StatusHMI.undefined := FALSE;
				timer(IN := FALSE);
				em.Config.Data.Q[1] := TRUE;
				State := 60;
			END_IF	
			
			// Main execute
		60:	IF em.Cmd = E_CMD.hold THEN
				em.StatusHMI.undefined := TRUE;
				em.Status := E_Status.holding;
				State := 200;
			ELSIF em.Cmd = E_CMD.suspend THEN
				em.StatusHMI.undefined := TRUE;
				em.Status := E_Status.suspending;
				State := 300;
			END_IF
			
			IF em.Config.Data.Q[2] AND em.Config.Data.I[1] THEN
				em.Config.Data.Q[2] := FALSE;
				bSliderSide := FALSE;
				State := 50;
			END_IF
			
			IF em.Config.Data.Q[1] AND em.Config.Data.I[2] THEN
				bSliderSide := TRUE;
				State := 70;
			END_IF
			
		70:	IF NOT em.Config.Data.I[3] THEN
				em.Config.Data.Q[1] := FALSE;
				State := 50;
			END_IF
			
		200:em.Status := E_Status.held;
			em.Config.Data.Q[1] := FALSE;
			em.Config.Data.Q[2] := FALSE;
			em.StatusHMI.undefined := TRUE;
			IF em.Cmd = E_CMD.unhold THEN
				em.Status := E_Status.unholding;
				State := 50;
			END_IF
			
		300:em.Status := E_Status.suspended;
			em.Config.Data.Q[1] := FALSE;
			em.Config.Data.Q[2] := FALSE;
			em.StatusHMI.undefined := TRUE;
			IF em.Cmd = E_CMD.unsuspend THEN
				em.Status := E_Status.unsuspending;
				State := 50;
			END_IF
	END_CASE
END_IF

em.SequenceStep := State;
em.Config.Data.objectSensor REF= em.Config.Data.I[3];

IF em.Config.Data.Q[1] XOR em.Config.Data.Q[2] THEN
	em.StatusHMI.isMoving := TRUE;
ELSIF em.Config.Data.Q[1] AND em.Config.Data.Q[2] THEN
	em.StatusHmi.isMoving := FALSE;
	em.StatusHMI.undefined := TRUE;
	em.StatusHMI.alarm := TRUE;
	em.StatusHMI.alarmText := 'Both actuators are operating';
ELSE
	em.StatusHmi.isMoving := FALSE;
	em.StatusHMI.undefined := FALSE;
	em.StatusHMI.alarm := FALSE;
	em.StatusHMI.alarmText := '';
END_IF

IF em.Config.Data.I[3] THEN
	em.StatusHMI.hasObject := TRUE;
ELSE
	em.StatusHMI.hasObject := FALSE;
END_IF

IF em.Config.Data.I[1] THEN
	em.StatusHMI.currentPosition := 1;
ELSIF em.Config.Data.I[2] THEN
	em.StatusHMI.currentPosition := 2;
ELSE
	em.StatusHMI.currentPosition := 0;
END_IF







]]></ST>
    </Implementation>
    <LineIds Name="fbSliderEM">
      <LineId Id="568" Count="0" />
      <LineId Id="571" Count="2" />
      <LineId Id="577" Count="2" />
      <LineId Id="808" Count="0" />
      <LineId Id="819" Count="0" />
      <LineId Id="818" Count="0" />
      <LineId Id="828" Count="0" />
      <LineId Id="836" Count="3" />
      <LineId Id="841" Count="0" />
      <LineId Id="840" Count="0" />
      <LineId Id="899" Count="0" />
      <LineId Id="569" Count="0" />
      <LineId Id="688" Count="10" />
      <LineId Id="945" Count="0" />
      <LineId Id="919" Count="0" />
      <LineId Id="699" Count="0" />
      <LineId Id="946" Count="0" />
      <LineId Id="700" Count="6" />
      <LineId Id="960" Count="2" />
      <LineId Id="707" Count="5" />
      <LineId Id="957" Count="1" />
      <LineId Id="713" Count="0" />
      <LineId Id="881" Count="2" />
      <LineId Id="714" Count="2" />
      <LineId Id="959" Count="0" />
      <LineId Id="717" Count="5" />
      <LineId Id="947" Count="1" />
      <LineId Id="723" Count="8" />
      <LineId Id="921" Count="2" />
      <LineId Id="732" Count="3" />
      <LineId Id="924" Count="0" />
      <LineId Id="736" Count="17" />
      <LineId Id="930" Count="0" />
      <LineId Id="900" Count="0" />
      <LineId Id="756" Count="1" />
      <LineId Id="912" Count="0" />
      <LineId Id="914" Count="0" />
      <LineId Id="936" Count="0" />
      <LineId Id="933" Count="0" />
      <LineId Id="955" Count="0" />
      <LineId Id="915" Count="2" />
      <LineId Id="913" Count="0" />
      <LineId Id="758" Count="0" />
      <LineId Id="937" Count="0" />
      <LineId Id="939" Count="0" />
      <LineId Id="903" Count="0" />
      <LineId Id="956" Count="0" />
      <LineId Id="910" Count="0" />
      <LineId Id="908" Count="1" />
      <LineId Id="905" Count="0" />
      <LineId Id="904" Count="0" />
      <LineId Id="759" Count="1" />
      <LineId Id="943" Count="0" />
      <LineId Id="761" Count="2" />
      <LineId Id="944" Count="0" />
      <LineId Id="764" Count="4" />
      <LineId Id="865" Count="0" />
      <LineId Id="770" Count="4" />
      <LineId Id="776" Count="3" />
      <LineId Id="863" Count="0" />
      <LineId Id="866" Count="0" />
      <LineId Id="859" Count="0" />
      <LineId Id="864" Count="0" />
      <LineId Id="856" Count="0" />
      <LineId Id="780" Count="2" />
      <LineId Id="952" Count="0" />
      <LineId Id="783" Count="7" />
      <LineId Id="954" Count="0" />
      <LineId Id="791" Count="8" />
      <LineId Id="232" Count="1" />
      <LineId Id="992" Count="0" />
      <LineId Id="965" Count="4" />
      <LineId Id="978" Count="0" />
      <LineId Id="980" Count="2" />
      <LineId Id="979" Count="0" />
      <LineId Id="964" Count="0" />
      <LineId Id="971" Count="0" />
      <LineId Id="970" Count="0" />
      <LineId Id="972" Count="0" />
      <LineId Id="974" Count="0" />
      <LineId Id="977" Count="0" />
      <LineId Id="973" Count="0" />
      <LineId Id="235" Count="1" />
      <LineId Id="983" Count="0" />
      <LineId Id="985" Count="3" />
      <LineId Id="984" Count="0" />
      <LineId Id="237" Count="6" />
      <LineId Id="228" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>